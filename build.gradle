buildscript {

    def a_user = hasProperty('artifactory_user') ? artifactory_user : System.getenv('artifactory_user')
    def a_password = hasProperty('artifactory_password') ? artifactory_password : System.getenv('artifactory_password')

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://dl.bintray.com/cbeust/maven'
        }
        maven {
            url 'https://oss.jfrog.org/artifactory/plugins-release'
            credentials {
                username = "${a_user}"
                password = "${a_password}"
            }
        }
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'org.hibernate.build.gradle:version-injection-plugin:1.0.0'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.9'
    id "com.jfrog.bintray" version "1.7.3"
    id "com.jfrog.artifactory" version "4.4.10"
    id "nebula.optional-base" version "3.1.0"
    id "nebula.provided-base" version "3.1.0"
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
    link 'GitHub', 'https://github.com/cbeust/testng/tree/master'
    if (System.getenv('CI')) {
        publishOnFailure()
        tag 'CI'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}

group = 'org.testng'
version = '6.14-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'osgi'
apply plugin: 'version-injection'
apply plugin: 'eclipse'

targetCompatibility = "1.7"
sourceCompatibility = "1.7"

eclipse {
    classpath {
        defaultOutputDir = file('target/classes')
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://dl.bintray.com/cbeust/maven'
    }
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'

    compile 'org.apache-extras.beanshell:bsh:2.0b6'
    compile 'com.beust:jcommander:1.66'

    compile 'org.apache.ant:ant:1.9.7'
    compile 'junit:junit:4.12', optional
    compile 'org.yaml:snakeyaml:1.17', optional

    provided 'com.google.inject:guice:4.1.0:no_aop'

    testCompile 'org.assertj:assertj-core:2.5.0'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.mockito:mockito-core:2.12.0'
}

task sourceJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    sourceJar
}

jar {
    manifest {
        instruction 'Bundle-License', 'http://apache.org/licenses/LICENSE-2.0'
        instruction 'Bundle-Description', 'TestNG is a testing framework.'
        instruction 'Import-Package',
            'bsh.*;version="[2.0.0,3.0.0)";resolution:=optional',
            'com.beust.jcommander.*;version="[1.7.0,3.0.0)";resolution:=optional',
            'com.google.inject.*;version="[1.2,1.3)";resolution:=optional',
            'junit.framework;version="[3.8.1, 5.0.0)";resolution:=optional',
            'org.junit.*;resolution:=optional',
            'org.apache.tools.ant.*;version="[1.7.0, 2.0.0)";resolution:=optional',
            'org.yaml.*;version="[1.6,2.0)";resolution:=optional',
            '!com.beust.testng',
            '!org.testng.*',
            '!com.sun.*',
            '*'
    }
}

versionInjection {
    into('org.testng.internal.Version', 'VERSION')
    into('org.testng.internal.Version', 'getVersionString')
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    testLogging.showStandardStreams = true
    systemProperties = System.getProperties()
    systemProperties['test.resources.dir'] = 'build/resources/test/'
    maxHeapSize = '1500m'
}

allprojects {
    tasks.withType(Javadoc) {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        options.links("https://docs.oracle.com/javase/7/docs/api")
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    apply plugin: 'org.sonarqube'
    sonarqube {
        properties {
            property "sonar.host.url", "https://sonarqube.com/"
            property "sonar.github.repository", "cbeust/testng"
            property "sonar.github.login", "testng-bot"
        }
    }

    sourceSets {
        test8 {
            java {
                srcDirs = ['src/test/java8']

            }
            resources {
                srcDirs = ['src/test/resources']
            }
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            sourceCompatibility = 1.8
            targetCompatibility = 1.8

        }
    }

    task testJava8(type: Test) {
        testClassesDir = sourceSets.test8.output.classesDir
        classpath += sourceSets.test8.runtimeClasspath

        useTestNG() {
            suites 'src/test/resources/testng-java8.xml'
        }
    }

    test.finalizedBy testJava8
}

apply from: 'gradle/publishing.gradle'
